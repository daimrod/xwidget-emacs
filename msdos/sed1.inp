# -sed1.inp-------------------------------------------------------------
# Configuration script for src/makefile
# ----------------------------------------------------------------------
#
# Copyright (C) 1993, 1994, 1995, 1996, 1997, 2000
#   Free Software Foundation, Inc.
#
# This file is part of GNU Emacs.
#
# GNU Emacs is free software; you can redistribute it and/or modify
# it under the terms of the GNU General Public License as published by
# the Free Software Foundation; either version 2, or (at your option)
# any later version.
#
# GNU Emacs is distributed in the hope that it will be useful,
# but WITHOUT ANY WARRANTY; without even the implied warranty of
# MERCHANTABILITY or FITNESS FOR A PARTICULAR PURPOSE.  See the
# GNU General Public License for more details.
#
# You should have received a copy of the GNU General Public License
# along with GNU Emacs; see the file COPYING.  If not, write to the
# Free Software Foundation, Inc., 59 Temple Place - Suite 330,
# Boston, MA 02111-1307, USA.

# In case src/Makefile needs to rebuild `make-doc.exe' (might happen under
# Windows 95), make sure it gets built with commands that DOS
# understands (one command per line).
s/^#.*//
s/^[ \f\t][ \f\t]*$//
s/^ /	/
s/\.h\.in/.h-in/
s!^	\./temacs!	go32 temacs!
s!/bin/sh!command.com!
/^MAKE *=/s/^/# /
/^SHELL *=/s/^/# /
/^srcdir *=/s/@[^@\n]*@/./
/^VPATH *=/s/@[^@\n]*@/./
/^CC *=/s/@[^@\n]*@/gcc/
/^CPP *=/s/@[^@\n]*@/gcc -e/
/^CFLAGS *=/s/@[^@\n]*@/-O2 -g/
/^LIBS *=/s/@[^@\n]*@//
/^LIBOBJS *=/s/@[^@\n]*@//
/^LN_S *=/s/@[^@\n]*@/ln -s/
/^M_FILE *=/s!@[^@\n]*@!m/intel386.h!
/^S_FILE *=/s!@[^@\n]*@!s/msdos.h!
/^@SET_MAKE@$/s/@SET_MAKE@//
/^.\${libsrc}make-docfile.*>/s!make-docfile!make-doc.exe -o ../etc/DOC!
/^.\${libsrc}make-doc/s!>.*$!!
/^\${libsrc}make-docfile:/c\
${libsrc}make-doc.exe:\
	cd ..\
	cd lib-src\
	$(MAKE) ${MFLAGS} make-docfile -W make-docfile.c\
	cd ..\
	cd src
/^	cd \${libsrc}; \${MAKE} \${MFLAGS} make-docfile/d
/\${libsrc}make-docfile/s/-docfile[ 	]/-doc.exe /
/^[ 	]*$/d
/^temacs:/s/prefix-args//
/^temacs:/s/stamp-oldxmenu//
/^	#/d
